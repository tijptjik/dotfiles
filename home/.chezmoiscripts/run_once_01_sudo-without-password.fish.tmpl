#!/usr/bin/env fish

# This script is based on the Gist by @jessfraz:
# https://gist.github.com/jessfraz/5232705

echo "[CONFIG] Passwordless Sudo..."

if not command -v -q sudo
    echo "sudo not found, skipping sudoers configuration."
    exit 0
end

set -l sudoers_dir /etc/sudoers.d
set -l username {{ .chezmoi.username }}
set -l sudoers_file "$sudoers_dir/wheel-nopasswd-{{ .chezmoi.username }}"

if test ! -d "$sudoers_dir"
    echo "sudoers.d directory not found, skipping."
    exit 0
end

# Check if the user is already in the wheel group
if not groups $username | grep -q '\bwheel\b'
    echo "Adding user $username to the wheel group."
    sudo usermod -aG wheel $username
else
    echo "User $username is already in the wheel group."
end

# Check if the sudoers file already exists
if test -e "$sudoers_file"
    echo "Sudoers file $sudoers_file already exists."
else
    echo "Creating sudoers file for the wheel group."
    # Use a temporary file to safely create the sudoers file
    set -l tmp_file (mktemp)
    echo '%wheel ALL=(ALL) NOPASSWD: ALL' >"$tmp_file"
    sudo chown root:root "$tmp_file"
    sudo chmod 0440 "$tmp_file"
    sudo mv "$tmp_file" "$sudoers_file"
end
