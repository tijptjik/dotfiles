#!/usr/bin/env fish
# Create a webdav user and directory

if test (count $argv) -eq 0; or string match -qr "^(-h|--help)$" $argv[1]
    echo "Usage: create-webdav-user <username>"
    echo ""
    echo "Creates a WebDAV user with a dedicated directory."
    echo "You will be prompted to enter a password securely."
    exit 0
end

set USER $argv[1]
set BASE_DIR "/mnt/storage/meta/appData/webdav"

if not string match -qr '^[a-zA-Z0-9_-]+$' $USER
    echo "Invalid username"
    exit 1
end

set USER_DIR "$BASE_DIR/$USER"

# Check if user directory already exists
if test -d "$USER_DIR"
    echo "Error: User directory already exists:"
    echo
    set_color blue
    echo "    $USER_DIR"
    set_color normal
    echo
    echo "Username already taken."
    exit 1
end

# Create .htpasswd entry first (will prompt for password)
if not sudo htpasswd /etc/nginx/.htpasswd "$USER"
    echo "Failed to set password. User creation aborted."
    exit 1
end

# Create directory
sudo mkdir -p "$USER_DIR"
sudo chmod 750 "$USER_DIR"
sudo chown nginx:media "$USER_DIR"  # Adjust nginx user if different

echo "User $USER created with directory:"
set_color blue
echo "$USER_DIR"
set_color normal
